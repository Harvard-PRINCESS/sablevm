AC_INIT(popt.h)
AC_CANONICAL_SYSTEM
AC_PREREQ(2.12)
AC_CONFIG_HEADERS(config.h)
AM_INIT_AUTOMAKE(popt-sable, 1.7)

AC_ISC_POSIX

AC_PROG_CC
AC_GCC_TRADITIONAL
AM_C_PROTOTYPES

dnl AM_DISABLE_SHARED
AM_PROG_LIBTOOL

AC_PROG_INSTALL

if test "X$CC" = Xgcc; then
    CFLAGS="-Wall $CFLAGS"
fi

dnl XXX lose rpm libs
LIBS=
addlib() {
  l=$1
  shift
  case "$target" in 
    *-*-solaris*)	LIBS="$LIBS -L$l -R $l $*";;
    *)		LIBS="$LIBS -L$l $*";;
  esac
}


AC_CHECK_HEADERS(alloca.h float.h libintl.h mcheck.h unistd.h)
AC_MSG_CHECKING(for /usr/ucblib in LIBS)
if test -d /usr/ucblib ; then
	if test "$build" = "mips-sni-sysv4" ; then
		addlib /usr/ccs/lib -lc
	fi

	addlib /usr/ucblib
	
	AC_MSG_RESULT(yes)
else
	AC_MSG_RESULT(no)
fi

AC_CHECK_FUNCS(strerror mtrace getuid geteuid)
AC_CHECK_FUNC(setreuid, [], [
    AC_CHECK_LIB(ucb, setreuid, [if echo $LIBS | grep -- -lucb >/dev/null ;then :; else LIBS="$LIBS -lc -lucb" USEUCB=y;fi])
])

AC_ARG_ENABLE(convenience-only-libpopt,
[  --enable-convenience-only-libpopt    only build convenience library])
AM_CONDITIONAL(CONV_ONLY_LIBPOPT,  test "$enable_convenience_only_libpopt" = "yes")

POPT_SOURCE_PATH="`pwd`"
AC_DEFINE_UNQUOTED(POPT_SOURCE_PATH, "$POPT_SOURCE_PATH",
	[Full path to popt top_sourcedir.])
AC_SUBST(POPT_SOURCE_PATH)

AC_OUTPUT([Makefile])
